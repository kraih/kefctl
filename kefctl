#!/usr/bin/perl
#
# Copyright (C) 2019-2020, Sebastian Riedel.
#
# This program is free software, you can redistribute it and/or modify it under
# the terms of the Artistic License version 2.0
#
use strict;
use warnings;
use 5.10.1;

use File::Spec;
use Getopt::Long 'GetOptions';
use IO::Socket::INET;
use Socket qw(IPPROTO_TCP TCP_NODELAY);

use constant DEBUG => $ENV{KEFCTL_DEBUG} || 0;

our $VERSION = '1.11';

my $GET_VOLUME = "\x47\x25\x80";
my $GET_SOURCE = "\x47\x30\x80";

my $PLAY = "\x53\x31\x81\x81";
my $NEXT = "\x53\x31\x81\x82";
my $PREV = "\x53\x31\x81\x83";

# Config file
my $HOST = '192.168.178.42';
if (-r (my $config = File::Spec->catfile($ENV{HOME}, '.kefctl'))) {
  open my $fh, '<', $config or die "Can't open file: $!";
  $HOST = <$fh>;
  chomp $HOST;
}

Getopt::Long::Configure(qw(default no_auto_abbrev no_ignore_case));
GetOptions
  'h|help'      => \my $help,
  'H|host=s'    => \$HOST,
  'i|input=s'   => \my $input,
  'I|inverse=s' => \my $inverse,
  'L|lower=i'   => \my $lower,
  'm|mute'      => \my $mute,
  'N|next'      => \my $next,
  'o|off'       => \my $off,
  'O|on'        => \my $on,
  'p|play'      => \my $play,
  'P|previous'  => \my $prev,
  'r|request=s' => \my $request,
  'R|raise=i'   => \my $raise,
  's|status'    => \my $status,
  'S|standby=i' => \my $standby,
  'T|port=s'    => \(my $port = 50001),
  'u|unmute'    => \my $unmute,
  'v|volume=i'  => \my $volume,
  'version'     => \my $version;

# Usage
if ($help) { _usage() }

# Version
elsif ($version) { say "kefctl $VERSION" }

# Arbitrary request in hex (for testing)
elsif ($request) { say unpack 'H*', _command(pack('H*', $request)) }

# Set input source, standby mode and inverse mode
elsif ($input || $inverse || defined $standby) {
  my $source_bits = unpack 'B*', substr(_command($GET_SOURCE), 3, 1);

  # Always power on
  substr $source_bits, 0, 1, '0';

  if ($inverse && (my $val = lc $inverse)) {
    if    ($val eq 'on')  { substr $source_bits, 1, 1, '1' }
    elsif ($val eq 'off') { substr $source_bits, 1, 1, '0' }
    else                  { die "Unknown value: $val\n" }
  }

  if (defined $standby) {
    if    ($standby eq '60') { substr $source_bits, 2, 2, '01' }
    elsif ($standby eq '0')  { substr $source_bits, 2, 2, '10' }
    elsif ($standby eq '20') { substr $source_bits, 2, 2, '10' }
    else                     { die "Unknown value: $standby\n" }
  }

  if ($input && (my $val = lc $input)) {
    if    ($input eq 'wifi')      { substr $source_bits, 4, 4, '0010' }
    elsif ($input eq 'usb')       { substr $source_bits, 4, 4, '1100' }
    elsif ($input eq 'bluetooth') { substr $source_bits, 4, 4, '1001' }
    elsif ($input eq 'aux')       { substr $source_bits, 4, 4, '1010' }
    elsif ($input eq 'optical')   { substr $source_bits, 4, 4, '1011' }
    else                          { die "Unknown value: $val\n" }
  }

  my $source = pack 'B*', $source_bits;
  _command("\x53\x30\x81$source");
}

# Play
elsif ($play) { _command($PLAY) }

# Next
elsif ($next) { _command($NEXT) }

# Previous
elsif ($prev) { _command($PREV) }

# Set volume
elsif (defined($volume) && $volume >= 0 && $volume <= 100) {
  _set_volume($volume);
}

# Raise volume
elsif ($raise) {
  my $target = _get_volume() + $raise;
  _set_volume($target > 100 ? 100 : $target);
}

# Lower volume
elsif ($lower) {
  my $target = _get_volume() - $lower;
  _set_volume($target < 0 ? 0 : $target);
}

# Mute
elsif ($mute) {
  my $current = _get_volume();
  _set_volume($current + 128) if $current <= 128;
}

# Unmute
elsif ($unmute) {
  my $current = _get_volume();
  _set_volume($current - 128) if $current >= 128;
}

# Turn on
elsif ($on) { _power(0) }

# Turn off
elsif ($off) { _power(1) }

# Status information (should be complete)
elsif ($status) {

  my $volume = _get_volume();
  my $muted  = 'No';
  if ($volume >= 128) {
    $muted = 'Yes';
    $volume -= 128;
  }
  say "Volume:  $volume%";
  say "Muted:   $muted";

  my $source_bits = unpack 'B*', substr(_command($GET_SOURCE), 3, 1);
  warn "*** SRC: $source_bits\n" if DEBUG;
  my $power   = substr($source_bits, 0, 1) ? 'Off' : 'On';
  my $inverse = substr($source_bits, 1, 1) ? 'On'  : 'Off';

  my $standby_bits = substr($source_bits, 2, 2);
  my $standby      = 'Unknown';
  if    ($standby_bits eq '00') { $standby = '20 Minutes' }
  elsif ($standby_bits eq '01') { $standby = '60 Minutes' }
  elsif ($standby_bits eq '10') { $standby = 'Never' }

  my $input_bits = substr($source_bits, 4, 4);
  my $input      = 'Unknown';
  if    ($input_bits eq '0010') { $input = 'Wifi' }
  elsif ($input_bits eq '1100') { $input = 'USB' }
  elsif ($input_bits eq '1001') { $input = 'Bluetooth (paired)' }
  elsif ($input_bits eq '1111') { $input = 'Bluetooth (unpaired)' }
  elsif ($input_bits eq '1010') { $input = 'Aux' }
  elsif ($input_bits eq '1011') { $input = 'Optical' }

  say "Source:  $input";
  say "Standby: $standby";
  say "Inverse: $inverse";
  say "Power:   $power";
}

# Usage
else { _usage() }

sub _command {
  my $req = shift;

  state $sock;
  unless ($sock) {
    $sock = IO::Socket::INET->new(
      PeerHost => $HOST,
      PeerPort => $port,
      Type     => SOCK_STREAM,
    ) or die "Cannot construct socket: $@";
    setsockopt $sock, IPPROTO_TCP, TCP_NODELAY, 1;
  }

  warn "*** REQ: @{[unpack 'H*', $req]}\n" if DEBUG;
  $sock->syswrite($req) or die "Cannot write command: $!";
  die "Could not read command response: $!"
    unless defined(my $read = $sock->sysread(my $res, 1024, 0));
  warn "*** RES: @{[unpack 'H*', $res]}\n" if DEBUG;

  return $res;
}

sub _get_volume { unpack 'C', substr(_command($GET_VOLUME), 3, 1) }

sub _set_volume {
  my $volume = shift;
  my $byte   = pack 'C', $volume;
  _command("\x53\x25\x81$byte");
}

sub _power {
  my $off = shift;

  my $source_bits = unpack 'B*', substr(_command($GET_SOURCE), 3, 1);

  # All KEF speakers seem to have a bug where the control server crashes when
  # they are powered off with a 20 minute standby time (so we always switch from
  # 20 to 60 before powering off)
  if ($off && substr($source_bits, 2, 2) eq '00') {
    substr $source_bits, 2, 2, '01';
    my $source = pack 'B*', $source_bits;
    _command("\x53\x30\x81$source");
  }

  substr $source_bits, 0, 1, $off ? '1' : '0';
  my $source = pack 'B*', $source_bits;
  _command("\x53\x30\x81$source");
}

sub _usage {
  say <<'EOF';
Usage: kefctl [OPTIONS]

    kefctl --status
    kefctl --volume 70
    kefctl --raise 5
    kefctl --lower 5
    kefctl --off
    kefctl --on
    kefctl --play
    kefctl -i optical
    kefctl -i bluetooth -S 20 -I on
    kefctl -H 192.168.178.52 -T 50001 -i aux
    kefctl -r 5330819b0b

  Options:
    -h, --help                  Show this summary of available options
    -H, --host <host>           Speaker host, defaults to 192.168.178.42
    -i, --input <source>        Set input source to aux, bluetooth, optical,
                                usb or wifi
    -I, --inverse <mode>        Set inverse L/R speakers to on or off
    -L, --lower <percentage>    Lower volume by X percent
    -m, --mute                  Mute speakers
    -N, --next                  Next track
    -o, --off                   Turn speakers off, the speakers can be turned
                                back on with the --on option or by setting an
                                input source with the --input option
    -O, --on                    Turn speakers on
    -p, --play                  Play or pause track
    -P, --previous              Previous track
    -r, --request <hex>         Send raw request in hex format and show response
                                (very useful for testing speaker features)
    -R, --raise <percentage>    Raise volume by X percent
    -s, --status                Show current speaker status
    -S, --standby <minutes>     Set standby time to 20, 60 or 0 (to turn standby
                                off), this option can only be used together with
                                the --input option
    -T, --port <port>           Speaker port, defaults to 50001
    -u, --unmute                Unmute speakers
    -v, --volume <percentage>   Set volume to a percentage value of 0-100, be
                                aware that every input source has its own volume
                                setting
        --version               Show version

  You can also set the KEFCTL_DEBUG environment variable to get diagnostics
  information printed to STDERR.
EOF
}
